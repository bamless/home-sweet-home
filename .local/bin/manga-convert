#!/usr/bin/env jstar

// Convert and rename comics provided as cbz using kcc-c2e (pip install KindleComicConverter).
// The program accepts a pattern of the form 'new name %n' and a list of .cbz comics.
// During conversion, the comics will be renamed using the provided pattern by substituting the '%n'
// with a chapter number extracted from the cbz file name. If no number can be found in the cbz
// filename, it will be skipped. The output is gonna be a new file in mobi format, ready to be
// imported by a kindle.

import io
import re
import sys

// Constants
var MOBI_EXT = "mobi"
var NUM_REGEX = "%d+"

class ConvertException is Exception 
    fun new(chapterName, errMsg)
        super("Error converting chapter {0}: {1}" % (chapterName, errMsg))
    end
end

fun kccC2E(fileName, title)
    var cmd = 'kcc-c2e -m -q -2 -p K578 -g 0.75 -r 1 -t "{0}" "{1}" 2>/dev/null 1>&2'
    return sys.system(cmd % (title, fileName)) == 0
end

fun convertAndRename(chapter, name)
    if !kccC2E(chapter, name) then
        raise ConvertException(chapter, "kcc-c2e failed")
    end

    // delete KCC cache
    sys.system("rm -rf /tmp/KCC* 2>/dev/null 1>&2")

    var ext = re.match(capther, "%.([^%.]+)$")
    var converted = re.gsub(chapter, ext, MOBI_EXT) if ext else chapter + "." + MOBI_EXT

    try
        io.rename(converted, name + '.' + MOBI_EXT)
    except io.IOException e
        raise ConvertException(chapter, e.err())
    end
end

if __name__ == "__main__" then
    fun printProgress(n, total, chapterName, newName)
        var perc = int(((n + 1) / total) * 100)
        print("[{0}%] converting {1} to {2}" % (perc, chapterName, newName))
    end

    if #argv == 0 then
        io.stderr.writeln("Usage: manga-convert pattern [chapters...]")
        sys.exit(-1)
    end

    var namePattern = argv.removeAt(0)
    if !re.match(namePattern, '%%n') then
        io.stderr.writeln("Name pattern `{0}` doesn't contain `%n`" % namePattern)
        sys.exit(-1)
    end

    for var n, chapterName in argv.enumerate() do
        var chapterNum = re.match(chapterName, NUM_REGEX)
        
        if !chapterNum then
            io.stderr.writeln("`{0}` doesn't contain a chapter number, skipping" % chapterName)
            continue
        end

        var newName = re.gsub(namePattern, "%%n", chapterNum)
        printProgress(n, #argv, chapterName, newName)

        try 
            convertAndRename(chapterName, newName)
        except ConvertException e
            io.stderr.writeln(e.err())
        end
    end

    print("Done")
end
